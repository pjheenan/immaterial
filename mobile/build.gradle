apply plugin: 'com.android.application'
apply plugin: 'jacoco'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    aaptOptions {
        noCompress 'txt'
        ignoreAssetsPattern "!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~"
    }
    lintOptions {
        quiet true
        checkReleaseBuilds false
        abortOnError false
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }

    defaultConfig {
        applicationId "com.philheenan.immaterial"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile project(':core')
    compile project(':presentation')
    compile project(':remote')
    compile project(':cache')

    androidTestCompile rootProject.ext.testLibraries.junit
    androidTestCompile rootProject.ext.testLibraries.mockito
    androidTestCompile(rootProject.ext.testLibraries.espresso) {
        exclude group: 'com.squareup', module: 'javawriter'
    }
    androidTestCompile rootProject.ext.testLibraries.runner
    androidTestCompile rootProject.ext.testLibraries.rules
}
